@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

find_dependency(kvtree REQUIRED)

# Some packages below do not have cmake package config files.
# Instead, we provide cmake find module files, like FindBBAPI.cmake.
# This way users who build with cmake don't have to write their own.
# The line below registers the current working directory with cmake
# so that it can find the Find*.cmake module files.
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")

if (@HAVE_DATAWARP@)
  find_dependency(DataWarp REQUIRED)
endif()

if (@HAVE_BBAPI@)
  find_dependency(BBAPI REQUIRED)
endif()

if (@HAVE_NNFDM@)
  find_dependency(nnfdm REQUIRED)
endif()

if (@MPI@)
  find_dependency(MPI REQUIRED)
endif()

find_dependency(ZLIB REQUIRED)

if (@HAVE_PTHREADS@)
  find_dependency(Threads REQUIRED)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/axlTargets.cmake")
